class SwigBean {
    /**
     * 模块名称
     */
    String mModuleName
    /**
     * 是否是c++,默认为false
     */
    boolean mIsCxx = false
    /**
     * 编译参数，默认为空
     */
    String mArgs = ""

    SwigBean(String moduleName, boolean isCxx, String args) {
        mModuleName = moduleName
        mIsCxx = isCxx
        mArgs = args
    }

    SwigBean(String moduleName) {
        mModuleName = moduleName
    }
}

/**
 * 生成的包名
 */
def swigPackage = "com.shenby.swig"

/**
 * 配置swig自动生成package
 * https://blog.csdn.net/chifu5720/article/details/102919849
 */
task runSwig() {
    doLast {
        println "stop runSwig"
    }
    doFirst {
        println "start runSwig"
    }
    doLast {
        ArrayList<SwigBean> swigBeanList = [
                new SwigBean("Unix", false, " "),
                new SwigBean("MyEnum", false, " "),
                new SwigBean("MyStruct", false, " "),

        ]

        def osName = System.getProperty("os.name").toLowerCase()
        println "osName = $osName"
        def isWindowsOs = osName.contains("windows")
        def projectDirPath = projectDir.absolutePath

        swigBeanList.forEach {
            addSwigTask(it, projectDirPath, swigPackage, isWindowsOs)
        }

    }
}

def addSwigTask(SwigBean bean, String projectDirPath, String swigPackage, boolean isWindows) {
    println "start swig ${bean.mModuleName}"

    def cmd = "swig"
    def cxx = bean.ismIsCxx()

    def javaOutDir = "${projectDirPath}/src/main/java/${swigPackage.replace(".", "/")}"
    def wrapName = bean.mIsCxx ? "_wrap.cxx" : "_wrap.c"
    def nativeFileName = "${projectDirPath}/src/main/jni/${bean.mModuleName}${wrapName}"
    def swigFilePath = "${projectDirPath}/src/main/jni/${bean.mModuleName}.i"

    if (isWindows) {
        //todo windows 还没有编写好脚本
        System.err.println("swig windows 未实现")

    } else {
        if (cxx) {
            runExec(cmd,
                    "-c++",
                    "-java",
                    "-package", swigPackage,
                    "-outdir", javaOutDir,
                    "-o", nativeFileName,
                    swigFilePath
            )
        } else {
            runExec(cmd,
                    "-java",
                    "-package", swigPackage,
                    "-outdir", javaOutDir,
                    "-o", nativeFileName,
                    swigFilePath
            )
        }
    }
    println "finish swig ${bean.mModuleName}"
}


def runExec(String cmd, String... param) {
    exec {
        commandLine(cmd)
        args(param)
    }
}
